// This build is not a complete project, but is used to generate a project.
// See: ProtobufPluginTestHelper.groovy
buildscript {
    repositories {
        maven { url 'https://maven.google.com' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

    apply plugin: 'com.android.library'
    apply plugin: 'com.google.protobuf'

repositories {
    maven { url 'https://maven.google.com' }
    maven { url "https://plugins.gradle.org/m2/" }
}

android {
    compileSdkVersion 26
    buildToolsVersion ext.ANDROID_BUILD_TOOL

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }

    // https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "1g"
        threadCount 1 // reduce predex thread count to limit memory usage
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${ext.PROTOC}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${ext.PROTOC_GEN_GRPC_JAVA}"
        }
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:${ext.PROTOC_GEN_JAVALITE}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            javalite { }
        }
        ofNonTest()*.plugins {
            grpc {
                // Options added to --grpc_out
                option 'lite'
            }
        }
    }
}

dependencies {
    implementation "javax.annotation:javax.annotation-api:${ext.JAVAX_ANNOTATION_API}"
    implementation "com.google.protobuf:protobuf-lite:${ext.PROTOBUF_LITE}"
    implementation "io.grpc:grpc-core:${ext.GRPC_CORE}"
    implementation "io.grpc:grpc-stub:${ext.GRPC_STUB}"
    implementation "io.grpc:grpc-okhttp:${ext.GRPC_OKHTTP}"
    implementation("io.grpc:grpc-protobuf-lite:${ext.GRPC_PROTOBUF_LITE}") {
        // Otherwise Android compile will complain "Multiple dex files define ..."
        exclude module: "protobuf-lite"
    }
    testImplementation "junit:junit:${ext.JUNIT}"
}
