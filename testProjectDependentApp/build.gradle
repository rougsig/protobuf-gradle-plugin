// A project that depends on another project and a published artifact, both of
// which include proto files. The included proto files are added to the
// --proto_path argument of protoc, so that the protos from this project can
// import them. However, these imported proto files will not be compiled in
// this project, since they have already been compiled in their own projects.

  apply plugin: 'java'
  apply plugin: 'com.google.protobuf'

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    implementation "com.google.protobuf:protobuf-java:${ext.PROTOBUF_JAVA}"
    implementation project(':testProjectJavaLibrary')

    testImplementation "junit:junit:${ext.JUNIT}"
}

protobuf.protoc {
    artifact = "com.google.protobuf:protoc:${ext.PROTOC}"
}

test.doLast {
  // This project has compiled only one proto file, despite that it imports
  // other proto files from dependencies.
  def generatedFiles = project.fileTree(protobuf.generatedFilesBaseDir + "/main")
  File onlyGeneratedFile = generatedFiles.singleFile
  assert 'Dependent.java' == onlyGeneratedFile.name
}
